/*
 * generated by Xtext 2.11.0
 */
package hu.bzolt.webdsl.excepgroup

import com.google.common.base.Strings
import hu.bzolt.webdsl.webDsl.Excep
import hu.bzolt.webdsl.webDsl.ExcepGroup
import hu.bzolt.webdsl.webDsl.WebDslPackage
import java.util.ArrayList
import org.eclipse.xtext.formatting2.FormatterRequest
import org.eclipse.xtext.formatting2.IFormattableDocument
import org.eclipse.xtext.xbase.formatting2.XbaseFormatter

class ExcepGroupFormatter extends XbaseFormatter
{
	def dispatch void format(ExcepGroup eg, extension IFormattableDocument document)
	{
		eg.regionFor.keyword("requests").prepend[noSpace]
		eg.regionFor.feature(WebDslPackage.Literals.ABSTRACT_ELEMENT__NAME).prepend[oneSpace]
		interior(
			eg.regionFor.keyword("{").surround[newLine],
			eg.regionFor.keyword("}").prepend[newLine],
			[indent]
		)
		eg.regionFor.keywords(",").forEach [
			prepend[noSpace]
			append[setNewLines(1, 1, 2)]
		]
		var exceps = eg.exceps.drop(0)
		while (!exceps.empty)
		{
			val group = new ArrayList<Excep>
			group += exceps.head
			var i = 1
			while (i < exceps.size && exceps.get(i).previousHiddenRegion.lineCount <= 2)
			{
				group += exceps.get(i)
				i++
			}

			val width = group.map[regionFor.feature(WebDslPackage.Literals.EXCEP__NAME).length].
				max + 1;
			for (e : group)
			{
				val region = e.regionFor.feature(WebDslPackage.Literals.EXCEP__NAME)
				region.append[space = Strings.repeat(" ", width - region.length)]
				e.regionFor.keyword("->").append[oneSpace]
			}

			exceps = exceps.drop(i)
		}
	}

	def init(FormatterRequest request)
	{
		initialize(request)
	}

	def res()
	{
		reset
	}
}
